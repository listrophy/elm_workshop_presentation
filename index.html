<!doctype html>
<html>
	<head>
		<meta charset="utf-8">
		<meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, user-scalable=no">

		<title>reveal.js</title>

		<link rel="stylesheet" href="css/reveal.css">
		<link rel="stylesheet" href="css/theme/solarized.css" id="theme">
		<link rel="stylesheet" href="css/custom.css">

		<!-- Theme used for syntax highlighting of code -->
		<link rel="stylesheet" href="lib/css/github.css">

		<!-- Printing and PDF exports -->
		<script>
			var link = document.createElement( 'link' );
			link.rel = 'stylesheet';
			link.type = 'text/css';
			link.href = window.location.search.match( /print-pdf/gi ) ? 'css/print/pdf.css' : 'css/print/paper.css';
			document.getElementsByTagName( 'head' )[0].appendChild( link );
		</script>
	</head>
	<body>
		<div class="reveal">
			<div class="slides">



<section>
  <h1>Elm for Rails Developers</h1>
  <h2>Instructor: Brad Grzesiak</h2>
  <p>@listrophy</p>
  <p>Please visit ellie-app.com</p>
</section>

<section data-markdown>
  <script type="text/template">
## About Me

* ~2 years with Elm
* CEO/co-founder of Bendyworks
* Former Aerospace Engineer
* Science Consultant
  </script>
</section>

<section data-markdown>
  <script type="text/template">
## Agenda

* Compare/Contrast with Ruby/Rails
* Elm Syntax (c.f. Ruby)
* Elm Semantics (c.f. Rails)
* Mini-Exercises
* SpaceElm!
  </script>
</section>

<section>
  <h1>Compare/Contrast with Ruby</h1>
</section>

<style>
  [data-columns] {
    display: flex !important;
    flex-wrap: wrap;
    justify-content: center;
    align-items: center;
  }
  [data-columns] h2 {
    flex: 1 0 100%;
  }
  [data-columns] .column {
    margin-left: 1.5em;
    margin-right: 1.5em;
  }
</style>

<section>
<h2>Some Similarities</h2>

<ul>
  <li class="fragment">Strings, Numbers, Dicts<sup>&#42;</sup>, Arrays<sup>&#42;</sup></li>
  <li class="fragment">Modules</li>
  <li class="fragment">Functions, but not <i>methods</i></li>
  <li class="fragment">Last expression is return value</li>
  <li class="fragment">Avoid parens where possible</li>
  <li class="fragment">(Rails) Directory Structure => Module Structure</li>
</ul>
</section>

<section>
  <h1>Some Differences</h1>
</section>

<section>
  <h2>Functions, not Methods</h2>
  <pre><code data-trim data-noescape class="ruby">
    [3, 1, 2].sort
      # => [1, 2, 3]
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
    List.sort [3, 1, 2]
      -- => [1, 2, 3]
  </code></pre>
</section>

<section>
  <h2>Functions, not Methods (pt 2)</h2>
  <pre><code data-trim data-noescape class="ruby">
    ["a", "b", "c"].join " "
      # => "a b c"
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
    String.join " " ["a", "b", "c"]
      -- => "a b c"
  </code></pre>

  <p>Note: No commas between args</p>
  <p>Note 2: "Data" acted upon is last arg</p>
</section>

<section>
  <h2>Everything is Immutable</h2>
  <pre><code data-trim data-noescape class="ruby">
    x = [3, 1, 2]
    y = x.sort  # Elm equivalent exists
    x.sort!     # Elm equivalent does not exist
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
    List.sort [3, 1, 2]
  </code></pre>

</section>

<section>
  <h2>Super Weird Indentation Conventions</h2>
  <p>Turns out, they're not actually that bad!</p>

  <pre><code data-trim data-noescape class="ruby">
aList = [
  foo,
  bar,
  baz
]
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
aList =
  [ foo
  , bar
  , baz
  ]
  </code></pre>
</section>

<section>
  <h2>Type Strictness</h2>

  <pre><code data-trim data-noescape class="ruby">
aNumber = 42
errorMessage =
  if aNumber
    "An error of type #{aNumber} occurred."
  end
  </code></pre>

  <pre class="fragment" data-fragment-index="1"><code data-trim data-noescape class="elm">
aNumber = 42
errorMessage =
  if aNumber <mark>/=</mark> 0 <mark>then</mark>
    "An error of type " ++ (<mark>toString</mark> aNumber) ++ " occurred."
  <mark>else</mark>
    ""
  </code></pre>

  <p class="fragment" data-fragment-index="1">
    <small>Note: There is no "truthy" or "falsey", only "True" or "False"<br>
      Note: No automatic string conversion<br>
      Note: All code-paths must yield same type<br>
      Note: Indentation matters!
    </small>
  </p>

</section>

<section>
  <h2>Explicit Type Signatures Are a Thing</h2>
  <pre><code data-trim data-noescape class="elm">
    aValue : String
    aValue =
      "foo"

    aFunc : String -> String
    aFunc stringArg =
      String.toUpper (String.trim stringArg)
  </code></pre>
</section>

<section>
  <h2>Importing is Not Transitive</h2>

  <pre><code data-trim data-noescape class="ruby">
    require 'foo'

    # Now everything from 'foo' is available,
    # INCLUDING everything that 'foo' itself required
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
    import Foo

    -- Now everything from Foo is available but still namespaced
    -- under "Foo."
    --
    -- Nothing that Foo imported in its own file is available here
    -- unless it's been explicitly "exposed"
  </code></pre>
</section>

<section>
  <h2>Exporting is Specified</h2>
    <pre><code data-trim data-noescape class="ruby">
      # in file "foo.rb"

      class Foo; end

      # files that require 'foo' have access to Foo
    </code></pre>

    <pre><code data-trim data-noescape class="elm">
      -- in file "Foo.elm"

      module Foo exposing (foo, foo2)

      -- files that "import Foo" can only access
      -- functions "foo" and "foo2"
    </code></pre>

    <pre><code data-trim data-noescape class="elm">
      -- in file "Foo.elm"

      module Foo exposing (..)

      -- files that import 'Foo' can access all functions in Foo
    </code></pre>

</section>

<section>
  <h2>Granular, Configurable Importing</h2>

  <pre><code data-trim data-noescape class="ruby">
    require 'foo'

    # Now everything from 'foo' is available,
    # INCLUDING everything that 'foo' itself required
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
    import Foo
    import Bar as B
    import Baz exposing (..)
    import Qux exposing (corge)

    Foo.func "a" -- call "func" from module Foo with arg "a"
    B.func "a" -- call "func" from module Bar with arg "a"
    bazzle "a" -- call "bazzle" with arg "a",
               -- which apparently came from Baz
    corge "a" -- call "corge" from module Qux, with arg "a"
  </code></pre>
</section>



<section>
  <h2>Case > If-Else</h2>


  <pre><code data-trim data-noescape class="ruby">
    if foo?
      doSomething
    else
      doOtherThing
    end
  </code></pre>


  <pre><code data-trim data-noescape class="elm">
    case foo of
      True ->
        doSomething

      False ->
        doOtherThing
  </code></pre>

  <p>Note: The compiler ensures case statements are exhaustive!</p>

</section>

<section>
  <h2>Views are Functions, Not Templates</h2>

  <pre><code data-trim class="erb">
    <% if current_user %>
      <%= button_to "Log out", destroy_session_path %>
    <% else %>
      <%= render partial: 'login' %>
    <% end %>
  </code></pre>


  <pre><code data-trim data-noescape class="elm">
    case model.currentUser of
      User _ ->
        Html.button
          [ onClick SignOut ]
          [ text "Log out" ]

      Anonymous ->
        loginView

      <span class="fragment">Admin _ ->
        adminSimulateUserOrLogoutView</span>
  </code></pre>
</section>

<section>
  <h2>Functions are First-Class</h2>

    <pre><code data-trim data-noescape class="js">
      var logDiv = function(index, div) {
        console.log(div.innerHTML);
      };

      $('div').each(logDiv);
    </code></pre>


    <pre><code data-trim data-noescape class="elm">
      List.map <mark>String.toUpper</mark> ["foo", "bar", "baz"]
    </code></pre>
</section>

<section>
  <h1>Syntax</h1>
</section>

<section>
  <h2>A Really Basic File</h2>
  <p>Just a grab-bag to show syntax:</p>
  <pre><code data-trim data-noescape class="elm">
    module Trilean
        exposing
            ( Reason
            , Trilean
            , isSure
            , reason
            )

    -- ...
  </code></pre>
</section>

<section>
  <pre><code data-trim data-noescape class="elm">
    -- ...

    type alias Reason =
        String

    type Trilean
        = Yes
        | No
        | Other Reason

    -- ...
  </code></pre>
</section>

<section>
  <pre><code data-trim data-noescape class="elm">
    -- ...

    <span class="fragment highlight-background-yellow">isSure : Trilean -> Bool</span>
    isSure trilean =
        case trilean of
            Yes ->
                True

            _ ->
                False

    -- ...
  </code></pre>
</section>

<section>
    <pre><code data-trim data-noescape class="elm">
    -- ...

    reason : Trilean -> Maybe String
    reason trilean =
        case trilean of
            Other str ->
                Just str

            _ ->
                Nothing

    -- ...
  </code></pre>
</section>

<section>
  <h2>Whoa Whoa Whoa... Maybe?</h2>

  <dl>
    <dt><code>String</code></dt>
    <dd>A <kbd>String</kbd> that can <strong>never</strong> be nil</dd>
    <dt><code>Maybe String</code></dt>
    <dd>A box containing either <kbd>Nothing</kbd> or <kbd>Just</kbd> a <kbd>String</kbd></dd>
  </dl>
  <hr class="fragment" data-fragment-index="1" />
  <p class="fragment" data-fragment-index="1"><strong>Important:</strong> You cannot use a <kbd>Maybe String</kbd> like
    a <kbd>String</kbd> any more than you can use an <kbd>Array</kbd> of <kbd>String</kbd>s like
    a single <kbd>String</kbd> in Ruby.
  </p>
</section>

<section>
  <h2>Speaking of Lists...</h2>

  <pre><code data-trim data-noescape class="elm">
    singleDigitInts : List Int
    singleDigitInts =
        [ 0, 1, 2, 3, 4, 5, 6, 7, 8, 9 ]
        -- or `List.range 0 9`

    doubleList : List Int -> List Int
    doubleList list =
        List.map <span class="fragment highlight-background-yellow">(\int -> 2 * int)</span> list
        -- in Ruby:
        -- list.map { |int| 2 * int }
  </code></pre>
</section>

<section>
  <h2>Regarding "map"</h2>

  <dl>
    <dt><kbd>Namespace.map f wrapper</kbd></dt>
    <dd>Perform the function "f" on the data inside "wrapper", in the way
      that makes sense for the <em>type</em> of the "wrapper" as redundantly
      indicated by "Namespace."</dd>
  </dl>
</section>

<section>
  <h2>Regarding "map"</h2>

  <dl>
    <dt><kbd>List.map f list</kbd></dt>
    <dd>Perform "f" on each item in the "list"</dd>
    <dt class="fragment" data-fragment-index="1"><kbd>Maybe.map f maybeThing</kbd></dt>
    <dd class="fragment" data-fragment-index="1">Perform "f" on the item in the "maybeThing," assuming it's not "Nothing."</dd>
    <dt class="fragment" data-fragment-index="2"><kbd>Dict.map f dictionary</kbd></dt>
    <dd class="fragment" data-fragment-index="2">Replace each value in "dictionary" with the result of applying "f" to each key-value pair.</dd>
  </dl>
</section>

<section>
  <h2>Typical "map" Signature</h2>

  <pre><code data-trim data-noescape class="elm">
    map : (a -> b) -> ConcreteType a -> ConcreteType b

    -- for List
    map : (a -> b) -> List a -> List b

    -- for Maybe
    map : (a -> b) -> Maybe a -> Maybe b

    -- Elm does not have Higher-Kinded Types, so this doesn't work:
    map&lt;M&gt; : (a -> b) -> M a -> M b
  </code></pre>
  <p>Note: Lack of Higher-Kinded Types in this situation means you can't have a generic
    <kbd>map</kbd> function that works on anything "mappable."</p>
</section>

<section>
  <h2><kbd>Let</kbd>'s Take it Easy</h2>

  <pre><code data-trim data-noescape class="elm">
    normalizeString : String -> String
    normalizeString input =
        <mark>let</mark>
            trimmed = String.trim input
            lower = String.toLower trimmed
        <mark>in</mark>
            String.padLeft 8 ' ' lower
  </code></pre>
</section>

<section>
  <h2>Guzinta!</h2>
  <p>ok, ok, "forward function application"</p>

  <pre><code data-trim data-noescape class="elm">
    normalizeString : String -> String
    normalizeString input =
        let
            trimmed = String.trim input
            lower = String.toLower trimmed
        in
            String.padLeft 8 ' ' lower

    alsoNormalizeString : String -> String
    alsoNormalizeString input =
        input
          |> String.trim
          |> String.toLower
          |> <span class="fragment highlight-background-yellow">String.padLeft 8 ' '</span>
  </code></pre>
</section>

<section>
  <h2>Partial Application</h2>

  <p>You can supply only <em>some</em> arguments to <strong>any</strong> function,
    though only from left-to-right.</p>

    <pre><code data-trim data-noescape class="elm">
      -- type signature of String.padLeft:
      padLeft : Int -> Char -> String -> String

      -- effective type signature of String.padLeft:
      padLeft : Int -> (Char -> (String -> String))
    </code></pre>

    <p>Therefore, if you know the padding width, but not the padding char or the string, you can do:</p>
    <pre><code data-trim data-noescape class="elm">
      String.padLeft 8
        -- &lt;function&gt; : Char -> String -> String
    </code></pre>
</section>

<section>
  <h2>What about Compositiion?</h2>

  <pre><code data-trim data-noescape class="elm">
    alsoNormalizeString : String -> String
    alsoNormalizeString input =
        input
          |> String.trim
          |> String.toLower
          |> String.padLeft 8 ' '

    moreNormalizeString : String -> String
    moreNormalizeStri<span class="fragment highlight-background-yellow">ng =</span>
      String.trim >> String.toLower >> String.padLeft 8 ' '
  </code></pre>
  <p>Note: This is kinda advanced and not completely necessary for the purposes
    of this workshop</p>
</section>

<section>
  <h2>Avoiding parens</h2>

  <pre><code data-trim data-noescape class="elm">
    parenNormalizeString : String -> String
    parenNormalizeString input =
        String.padLeft 8 ' ' (String.toLower (String.trim input))

    anotherNormalizeString : String -> String
    anotherNormalizeString input =
        String.padLeft 8 ' ' <| String.toLower <| String.trim input
  </code></pre>
  <p>Note: Much like in Ruby, the balance between using and not using parens
    is subject to much debate</p>
</section>

<section>
  <h2>Primitives!</h2>

  <pre><code data-trim data-noescape class="elm">
    x = 42              -- Int
    y = 3.14            -- Float
    c = '.'             -- Char (rarely used)
    s = "hello world"   -- String
    l = []              -- List a
    t = (1, "foo", 4.0) -- Tuple of type (Int, String, Float)

    -- Record of type { foo : String, bar : Int }
    r = { foo = "foo", bar = 123}
  </code></pre>
</section>

<section>
  <h2>Common, Built-in Types</h2>

  <pre><code data-trim data-noescape class="elm">
    -- Bool
    b1 = True
    b2 = False

    -- Maybe a
    -- here, "Maybe String"
    m1 = Just "a string"
    m2 = Nothing

    -- Result a b
    -- here, "Result String Int"
    r1 = Ok 42
    r2 = Err "error message"
  </code></pre>
</section>

<section>
  <h2>Type Definitions</h2>

  <pre><code data-trim data-noescape class="elm">
    -- ours
    type Trilean
      = Yes
      | No
      | Other Reason

    -- builtin
    type Result a b
      = Err a
      | Ok b

    type Maybe a
      = Just a
      | Nothing
  </code></pre>
</section>

<section>
  <h2>Types vs. Values of Types</h2>

  <pre><code data-trim data-noescape class="elm">
    type Maybe a
      = Just a
      | Nothing

    -- Type constructors
    Just : a -> Maybe a
    Nothing : Maybe a

    type Result a b
      = Err a
      | Ok b
  </code></pre>
</section>

<section>
  <h2>Using Type Signatures</h2>

  <pre><code data-trim data-noescape class="elm">
    -- List
    first : List a -> Maybe a

    -- String
    join : String -> List String -> String

    -- Json.Decode
    decodeString : Decoder a -> String -> Result String a

    -- List
    filter : (a -> Bool) -> List a -> List a
  </code></pre>
</section>

<section>
  <h2>Semantics</h2>
  <h3>(Comparing to Rails)</h3>
</section>

<section>
  <h2>Model-View-Controller</h2>
  <h3>Yeah, really!</h3>

  <pre><code data-trim data-noescape class="elm">
    main =
        Html.beginnerProgram
            <span class="fragment __highlight-current" data-fragment-index="1">{ <span class="fragment __highlight-current" data-fragment-index="2">model  = model</span>
            , <span class="fragment __highlight-current" data-fragment-index="3">view   = view</span>
            , <span class="fragment __highlight-current" data-fragment-index="4">update = update</span>
          }</span>

    <span class="fragment __highlight-current" data-fragment-index="2">model : Model
    model = ...</span>

    <span class="fragment __highlight-current" data-fragment-index="3">view : Model -> Html Msg
    view model = ...</span>

    <span class="fragment __highlight-current" data-fragment-index="4">update : Msg -> Model -> Model
    update msg model = ...</span>
  </code></pre>
</section>

<section>
  <h2>Advanced MVC</h2>

  <pre><code data-trim data-noescape class="elm">
    main =
        Html.<span class="fragment __highlight-current" data-fragment-index="0">program</span>
            { <span class="fragment __highlight-current" data-fragment-index="1">init = init</span>
            , view = view
            , <span class="fragment __highlight-current" data-fragment-index="2">update = update</span>
            , <span class="fragment __highlight-current" data-fragment-index="3">subscriptions = subscriptions</span>
            }

    init : <span class="fragment __highlight-current" data-fragment-index="1">(</span>Model<span class="fragment __highlight-current" data-fragment-index="1">, <span class="fragment __highlight-current" data-fragment-index="4">Cmd Msg</span>)</span>
    view : Model -> <span class="fragment __highlight-current" data-fragment-index="4">Html Msg</span>
    update : <span class="fragment __highlight-current" data-fragment-index="5">Msg</span> -> Model -> <span class="fragment __highlight-current" data-fragment-index="2">(</span>Model<span class="fragment __highlight-current" data-fragment-index="2">, <span class="fragment __highlight-current" data-fragment-index="4">Cmd Msg</span>)</span>
    <span class="fragment __highlight-current" data-fragment-index="3">subscriptions : Model -> <span class="fragment __highlight-current" data-fragment-index="4">Sub Msg</span></span>
  </code></pre>
</section>

<section>
  <h1>Diagram Time!</h1>
</section>

<section>
  <h2>Init</h2>

  <img src="img/init.png"/>
</section>

<section>
  <h2>Init</h2>

  <pre><code data-trim data-noescape class="elm">
    type alias Model =
        { username : String
        }

    init : (Model, Cmd Msg)
    init =
        ( { username = "" }
        , fetchUserList
        )

    fetchUserList : Cmd Msg
    fetchUserList =
        Http.send ...
  </code></pre>
</section>



<section>
  <h2>Update</h2>

  <img src="img/update.png"/>
</section>

<section>
  <h2>Update</h2>

  <pre><code data-trim data-noescape class="elm">
    type alias Model =
        { username : String
        }

    type Msg
        = UsernameChanged String
        | ...

    update : Msg -> Model -> (Model, Cmd Msg)
    update msg model =
        case msg of
            UsernameChanged newUsername ->
                ({model | username = newUsername}, Cmd.none)
            ... ->
                ...
  </code></pre>
</section>



<section>
  <h2>On Model Change</h2>

  <img src="img/model_change.png"/>
</section>

<section>
  <h2>On Model Change</h2>

  <pre><code data-trim data-noescape class="elm">
    subscriptions : Model -> Sub Msg
    subscriptions model =
        if model.showClock then
            Time.every (1 * second) TimerTicked
        else
            Sub.none

    view : Model -> Html Msg
    view model =
        Html.button
            [ onClick CalculateClicked ]
            [ text "do a calculation" ]
  </code></pre>
</section>



<section>
  <h2>Sending Msg's</h2>

  <img src="img/send_msg.png"/>
</section>

<section>
  <h2>Sending Msg's</h2>

  <pre><code data-trim data-noescape class="elm">
    -- some "typical" Msg values
    type Msg
      = UsernameChanged String                 -- Html Msg
      <span class="fragment">| FieldChanged String String             -- Html Msg</span>
      <span class="fragment">| CalculateClicked                       -- Html Msg</span>
      <span class="fragment">| TimerTicked Time                       -- Sub Msg</span>
      <span class="fragment">| RandomNumberChosen Int                 -- Cmd Msg</span>
      <span class="fragment">| UserListRecieved (WebData (List User)) -- Cmd Msg</span>
  </code></pre>
</section>

<section>
  <h2>Summary</h2>

  <dl>
    <dt>init : (Model, Cmd Msg)</dt>
    <dd>Called on page load. First model, trigger first <kbd>Cmd</kbd>s.</dd>
    <dt class="fragment" data-fragment-index="1">view : Model -> Html Msg</dt>
    <dd class="fragment" data-fragment-index="1">Called on model change. Generates HTML.</dd>
    <dt class="fragment" data-fragment-index="2">subscriptions : Model -> Sub Msg</dt>
    <dd class="fragment" data-fragment-index="2">Called on model change. Refreshes <kbd>Sub</kbd> list.</dd>
    <dt class="fragment" data-fragment-index="3">update : Msg -> Model -> (Model, Cmd Msg)</dt>
    <dd class="fragment" data-fragment-index="3">Called when Elm send a <kbd>Msg</kbd>. Updates model, sends <kbd>Cmd</kbd>s.</dd>
  </dl>
</section>


<section>
  <h1>Mini-Exercises</h1>
</section>

<section>
  <h2>Reference:</h2>
  <p style="text-align: center;">package.elm-lang.org</p>
</section>

<section>

<section>
  <h1>Score Keeper</h1>
</section>

<section>
  <h2>Score Keeper Definition Statement</h2>

  <p>In order to track my team's score<br/>
    As a score keeper<br/>
    I want to increment their score</p>
</section>

<section>
  <h2>Score Keeper</h2>
  <pre><code data-trim data-noescape class="elm">
    module Main exposing (main)

    import Html exposing (..)

    main =
        beginnerProgram
            { model = initialModel
            , view = view
            , update = update
            }
	</code></pre>
</section>

<section>
  <h2>Score Keeper Model</h2>

  <pre><code data-trim data-noescape class="elm">
    type alias Model = Int

    initialModel : Model
    initialModel =
        0
  </code></pre>

</section>


<section>
  <h2>Score Keeper View</h2>

  <pre><code data-trim data-noescape class="elm">
    -- insert at top:
    import Html.Events exposing (onClick)

    --

    type Msg
        = Increment

    view : Model -> Html Msg
    view model =
        div
            []
            [ button [ onClick Increment ] [ text "+" ]
            , text (toString model)
            ]
  </code></pre>

</section>


<section>
  <h2>Score Keeper Update</h2>

  <pre><code data-trim data-noescape class="elm">
    update : Msg -> Model -> Model
    update msg model =
        case msg of
            Increment ->
                model + 1
  </code></pre>

</section>

<section>
  <h1>Done!</h1>
</section>

</section>

<section>

<section>
  <h1>Change: Basketball</h1>
</section>

<section>
  <h2>Basketball</h2>
  <p>In order to track my basketball team's score<br/>
    As my team's scorekeeper<br/>
    I want to increment the score by 1, 2 or 3
  </p>
</section>

<section>
  <h2>Basketball Changes: Model</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    -- No change!
    type alias Model = Int

    initialModel : Model
    initialModel =
        0
  </code></pre>
</section>


<section>
  <h2>Basketball Changes: Msg</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    type Msg
        = FreeThrow
        | JumpShot
        | ThreePointer
  </code></pre>
</section>

<section>
  <h2>Basketball Changes: View</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    view : Model -> Html Msg
    view model =
        div
            []
            [ button [ onClick FreeThrow ] [ text "free throw" ]
            , button [ onClick JumpShot ] [ text "jump shot" ]
            , button [ onClick ThreePointer ] [ text "trey" ]
            , text (toString model)
            ]
  </code></pre>
</section>

<section>
  <h2>Basketball Changes: Update</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    update : Msg -> Model -> Model
    update msg model =
        case msg of
            FreeThrow ->
                model + 1
            JumpShot ->
                model + 2
            ThreePointer ->
                model + 3
  </code></pre>
</section>

<section>
  <h1>Done!</h1>
</section>


</section>

<section>



<section>
  <h1>Refactor</h1>
</section>

<section>
  <h2>Refactor</h2>
  <p><kbd>FreeThrow</kbd>, <kbd>JumpShot</kbd>, and <kbd>ThreePointer</kbd> seem
    a bit redundant. Let's use one <kbd>Msg</kbd> to represent all three.</p>
</section>


<section>
  <h2>Refactor: Model</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    -- No change!
    type alias Model = Int

    initialModel : Model
    initialModel =
        0
  </code></pre>
</section>


<section>
  <h2>Refactor: Msg</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    type Msg
        = AddPoints Int
  </code></pre>
</section>

<section>
  <h2>Refactor: View</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    view : Model -> Html Msg
    view model =
        div
            []
            [ button [ onClick (AddPoints 1) ] [ text "free throw" ]
            , button [ onClick (AddPoints 2) ] [ text "jump shot" ]
            , button [ onClick (AddPoints 3) ] [ text "trey" ]
            , text (toString model)
            ]
  </code></pre>
</section>

<section>
  <h2>Refactor: Update</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    update : Msg -> Model -> Model
    update msg model =
        case msg of
            AddPoints int ->
                model + int
  </code></pre>
</section>

<section>
  <h1>Done!</h1>
</section>




</section>

<section>



<section>
  <h1>Oops!</h1>
</section>

<section>
  <h2>Undo</h2>
  <p>I accidentally pressed the wrong button! I need to undo a score.</p>
</section>


<section>
  <h2>Refactor: Model</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    type alias Model = List Int

    initialModel : Model
    initialModel =
        []
  </code></pre>
</section>


<section>
  <h2>Refactor: Msg</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    type Msg
        = AddPoints Int
        | Undo
  </code></pre>
</section>

<section>
  <h2>Refactor: View</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    view : Model -> Html Msg
    view model =
        div
            []
            [ button [ onClick (AddPoints 1) ] [ text "free throw" ]
            , button [ onClick (AddPoints 2) ] [ text "jump shot" ]
            , button [ onClick (AddPoints 3) ] [ text "trey" ]
            , text (toString <mark>(List.sum model)</mark>)
            , button [ onClick Undo ] [ text "undo!" ]
            ]
  </code></pre>
</section>

<section>
  <h2>Refactor: Update</h2>

  <pre class="fragment"><code data-trim data-noescape class="elm">
    update : Msg -> Model -> Model
    update msg model =
        case msg of
            AddPoints int ->
                int :: model
            Undo ->
                List.drop 1 model
  </code></pre>
</section>

<section>
  <h1>Done!</h1>
</section>




</section>

<section>



<section>
  <h1>Fresh Start: A Clock</h1>
</section>

<section>
  <h2>A Clock</h2>
  <p>Let's move away from "beginnerProgram"</p>
</section>


<section>
  <h2>A Clock</h2>

  <pre><code data-trim data-noescape class="elm">
    module Main exposing (main)

    import Html exposing (..)
    import Date
    import Time

    main =
        program
            { init = init
            , view = view
            , update = update
            , subscriptions = subscriptions
            }
  </code></pre>
</section>


<section>
  <h2>Clock: Model &amp; Init</h2>

  <pre><code data-trim data-noescape class="elm">
    type alias Model = Maybe Time.Time

    init : (Model, Cmd Msg)
    init =
        ( Nothing
        , Cmd.none
        )
  </code></pre>
</section>


<section>
  <h2>Clock: Msg</h2>

  <pre><code data-trim data-noescape class="elm">
    type Msg
        = Tick Time.Time
  </code></pre>
</section>

<section>
  <h2>Clock: Subscriptions</h2>

  <pre><code data-trim data-noescape class="elm">
subscriptions : Model -> Sub Msg
subscriptions model =
    Time.every (100 * Time.millisecond) Tick
  </code></pre>
</section>

<section>
  <h2>Clock: Update</h2>

  <pre><code data-trim data-noescape class="elm">
update : Msg -> Model -> (Model, Cmd Msg)
update msg model =
    case msg of
        Tick t ->
            (Just (Date.fromTime t), Cmd.none)
  </code></pre>
</section>

<section>
  <h2>Clock: View</h2>

  <pre><code data-trim data-noescape class="elm">
view : Model -> Html Msg
view model =
    div [] <mark><|</mark>
        case model of
            Just d ->
                [ text <| toString (Date.hour d)
                , text ":"
                , text <| toString (Date.minute d)
                , text ":"
                , text <| toString (Date.second d)
                ]

            Nothing ->
                [ text "don't know yet!" ]
  </code></pre>
</section>


<section>
  <h1>Done!</h1>
</section>




</section>

<section>




<section>
  <h1>Random GIF Fetcher</h1>
</section>

<section>
  <h2>Random GIF Fetcher</h2>
  <p>Let's talk to an API!</p>
  <p>https://github.com/Giphy/GiphyAPI</p>
</section>


<section>
  <h2>GIF Fetcher</h2>

  <pre><code data-trim data-noescape class="elm">
module Main exposing (..)

import Html exposing (..)
import Html.Attributes exposing (src)
import Html.Events exposing (onClick)
import Http
import Json.Decode as JD

main =
    program
        { init = init
        , view = view
        , update = update
        , subscriptions = (\model -> Sub.none)
        }
  </code></pre>
</section>


<section>
  <h2>GIF Fetcher: Model &amp; Init</h2>

  <pre><code data-trim data-noescape class="elm">
type alias Model =
    { url : Maybe String
    , error : Maybe String
    }


init : ( Model, Cmd Msg )
init =
    ( <span class="fragment highlight-background-yellow">Model Nothing Nothing</span>, Cmd.none )
  </code></pre>
</section>


<section>
  <h2>GIF Fetcher: Msg</h2>

  <pre><code data-trim data-noescape class="elm">
type Msg
    = Fetch
    | Display (Result Http.Error String)
  </code></pre>
</section>

<section>
  <h2>GIF Fetcher: View</h2>

  <pre><code data-trim data-noescape class="elm">
view : Model -> Html Msg
view model =
    div []
        [ button [ onClick Fetch ] [ text "Fetch!" ]
        , text <| Maybe.withDefault "" model.error
        , <span class="fragment highlight-background-yellow">imageView</span> model.url
        ]
  </code></pre>
</section>

<section>
  <h2>GIF Fetcher: View (pt 2)</h2>

  <pre><code data-trim data-noescape class="elm">
imageView : Maybe String -> Html Msg
imageView mString =
    case mString of
        Just url ->
            img [ src url ] []

        Nothing ->
            text "not fetched"
  </code></pre>
</section>

<section>
  <h2>GIF Fetcher: Update</h2>

  <pre><code data-trim data-noescape class="elm">
update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        Fetch ->
            ( model, <span class="fragment highlight-background-yellow">doFetch</span> )

        Display (Ok url) ->
            ( { model | url = Just url, error = Nothing }
            , Cmd.none
            )

        Display (Err err) ->
            ( { model | error = Just <| toString err }
            , Cmd.none
            )
  </code></pre>
</section>

<section>
  <h2>GIF Fetcher: Update (pt 2)</h2>

  <pre><code data-trim data-noescape class="elm">
randomUrl : String
randomUrl =
    "https://api.giphy.com/v1/gifs/random?api_key=dc6zaTOxFJmzC&rating=g"

doFetch : Cmd Msg
doFetch =
    let
        decoder =
            JD.at [ "data", "fixed_height_small_url" ] JD.string
    in
        Http.get randomUrl decoder
            |> Http.send Display
  </code></pre>
</section>


<section>
  <h1>Done!</h1>
</section>


</section>

<section>
  <h1>Space-Elm!</h1>
</section>

<section>
  <h2>Space-Elm</h2>
  <p>A multiplayer, cooperative game to<br>survive incoming fields of asteroids</p>
</section>

<section>
  <h2>Space-Elm</h2>

  <img src="img/space-elm.png"/>
</section>

<section>
  <h2>Modeling the Problem</h2>

  <ul>
    <li>Me (<kbd>Float</kbd>)</li>
    <li>Laser (<kbd>Maybe Position</kbd>)</li>
    <li>Asteroids (<kbd>List Position</kbd>)</li>
    <li>Score (<kbd>Maybe Int</kbd>)</li>
    <li>Error (<kbd>Maybe String</kbd>)</li>
    <li>ActionCable (<kbd>ActionCable</kbd>)</li>
    <li>KeysDown (<kbd>Set KeyCode</kbd>)</li>
  </ul>
</section>

<section>
  <h2>Types</h2>
  <pre><code data-trim data-noescape class="elm">
type alias Model =
    { myPosition : Float
    , laser : Maybe Position
    , asteroids : List Position
    , score : Maybe Int
    , error : Maybe String
    , cable : ActionCable Msg
    , keysDown : Set.Set Keyboard.KeyCode
    }

type alias Position =
    { x : Float
    , y : Float
    }
  </code></pre>
</section>

<section>
  <h2>Modeliing the Events</h2>

  <pre><code data-trim data-noescape class="elm">
type Msg
    = CableMsg ACMsg.Msg
    <span class="fragment">| Tick Time.Time</span>
    <span class="fragment">| CableConnected ()</span>
    <span class="fragment">| KeyDown Keyboard.KeyCode</span>
    <span class="fragment">| KeyUp Keyboard.KeyCode</span>
    <span class="fragment">| DataReceived ID.Identifier JD.Value</span>
    <span class="fragment">| GenerateAsteroid Position</span>
  </code></pre>
</section>

<section>
  <h2>Handling the Events</h2>

  <pre><code data-trim data-noescape class="elm">
update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        CableMsg msg_ -> ...
        CableConnected _ -> ...
        Tick time -> ...
        <mark>KeyDown 32</mark> -> ...
        KeyDown keyCode -> ...
        KeyUp keyCode -> ...
        DataReceived _ jsonValue -> ...
        GenerateAsteroid position -> ...
</code></pre>
</section>

<section>
  <h2>Starting the game</h2>

  <ol>
    <li class="fragment"><kbd>requestAnimationFrame</kbd> Sub fires</li>
    <li class="fragment">It fires our <kbd>Tick</kbd> <kbd>Msg</kbd></li>
    <li class="fragment">We render the view based on the current model</li>
  </ol>

  <p class="fragment">That's it!<br><small>(we do score handling on the server asynchronously)</small></p>
</section>

<section>
  <h2>What's Left?</h2>

  <ul>
    <li>Actually do the <kbd>subscriptions</kbd></li>
    <li>Implement each <kbd>update</kbd> case</li>
    <li>Write the view</li>
  </ul>
</section>

<section>
  <h2>Subscriptions</h2>

  <pre><code data-trim data-noescape class="elm">
subscriptions : Model -> Sub Msg
subscriptions model =
    Sub.batch
        [ ActionCable.listen CableMsg model.cable
        , AF.diffs Tick
        , Keyboard.downs KeyDown
        , Keyboard.ups KeyUp
        ]
  </code></pre>
</section>

<section>
  <h2>Updates</h2>

  <pre><code data-trim data-noescape class="elm">
update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        CableMsg msg_ -> ...
        CableConnected _ -> ...
        Tick time -> ...
        <mark>KeyDown 32</mark> -> ...
        KeyDown keyCode -> ...
        KeyUp keyCode -> ...
        DataReceived _ jsonValue -> ...
        GenerateAsteroid position -> ...
</code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>CableMsg</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
    CableMsg msg_ ->
        ActionCable.update msg_ model.cable
            |> Tuple.mapFirst (\c -> { model | cable = c })

    -- or

    CableMsg msg_ ->
        let
            (newCable, cmd) =
                ActionCable.update msg_ model.cable
        in
            ( { model | cable = newCable }, cmd)
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>CableConnected</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
        CableConnected _ ->
            joinGame model
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
joinGame : Model -> ( Model, Cmd Msg )
joinGame model =
    case <mark>subscribeToGame model</mark> of
        Ok model_cmd ->
            model_cmd

        Err err ->
            ( { model
                | error = Just <| ActionCable.errorToString err
              }
            , Cmd.none
            )
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>CableConnected</kbd> (pt 2)</h3>

  <pre><code data-trim data-noescape class="elm">
subscribeToGame :
    Model
    -> Result ActionCable.ActionCableError ( Model, Cmd Msg )
subscribeToGame model =
    let
        setCable ( newCable, cmd ) =
            ( { model | cable = newCable }, cmd )
    in
        ActionCable.subscribeTo identifier model.cable
            |> Result.map setCable


identifier : ID.Identifier
identifier =
    ID.newIdentifier "GamesChannel" []
  </code></pre>
</section>


<section>
  <h2>Updates</h2>
  <h3><kbd>KeyDown</kbd> and <kbd>KeyUp</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
        KeyDown 32 ->
            ( fire model, Cmd.none )

        KeyDown keyCode ->
            ( { model | keysDown = Set.insert keyCode model.keysDown }
            , Cmd.none
            )

        KeyUp keyCode ->
            ( { model | keysDown = Set.remove keyCode model.keysDown }
            , Cmd.none
            )
  </code></pre>
</section>


<section>
  <h2>Updates</h2>
  <h3><kbd>KeyDown</kbd> and <kbd>KeyUp</kbd> (pt 2)</h3>

  <pre><code data-trim data-noescape class="elm">
fire : Model -> Model
fire <mark>({ laser, myPosition } as model)</mark> =
    let
        newLaser =
            Just <| Position leftEdge (2.5 * myPosition)
    in
        case laser of
            Nothing ->
                { model | laser = newLaser }

            Just realLaser ->
                if realLaser.x < 500 then
                    model
                else
                    { model | laser = newLaser }
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
        Tick time ->
            tick time model
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
tick : Time.Time -> Model -> ( Model, Cmd Msg )
tick time model =
    model
        |> moveShipAndLaser
        |> moveAsteroids
        |> blowUpAsteroid
        |> postScore
        |> (\( m, a, cmd ) -> ( m, regenerateAsteroids a cmd ))
  </code></pre>
</section>


<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>moveShipAndLaser</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
moveShipAndLaser : Model -> Model
moveShipAndLaser ({ myPosition } as model) =
    let
        moveMe =
            if Set.member 38 model.keysDown then
                2.0
            else if Set.member 40 model.keysDown then
                -2.0
            else
                0.0
    in
        { model
            | myPosition = myPosition + moveMe
            , laser = Maybe.map <span class="fragment __highlight-current">moveLaser</span> model.laser
        }
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>moveLaser</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
moveLaser : Position -> Position
moveLaser { x, y } =
    { x = x + 10, y = y }
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>moveAsteroids</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
moveAsteroids : Model -> ( Model, List Position )
moveAsteroids model =
    let
        move { x, y } =
            { x = x - 1, y = y }

        ( kept, past ) =
            model.asteroids
                |> List.map move
                |> List.partition (\pos -> pos.x > leftEdge - 50)
    in
        ( { model | asteroids = kept }, past )
  </code></pre>
</section>



<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>blowUpAsteroid</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
blowUpAsteroid :
    ( Model, List Position )
    -> ( Model, List Position, List Position )
blowUpAsteroid ( model, past ) =
    case model.laser of
        Nothing ->
            ( model, past, [] )

        Just laser ->
            ...
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>blowUpAsteroid</kbd> pt 2</h3>

  <pre><code data-trim data-noescape class="elm">
        Just laser ->
            let
                ( blownUp, keep ) =
                    List.partition
                        (detectCollision laser)
                        model.asteroids
            in
                if List.length blownUp > 0 then
                    ( { model | asteroids = keep, laser = Nothing }
                    , past
                    , blownUp
                    )
                else
                    ( model, past, [] )
  </code></pre>
</section>



<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>detectCollision</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
detectCollision : Position -> Position -> Bool
detectCollision laser asteroid =
    asteroidRadius >
        (sqrt <|
            (asteroid.x - laser.x) ^ 2
                + (asteroid.y - laser.y) ^ 2
        )
  </code></pre>
</section>


<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>postScore</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
postScore :
    ( Model, List Position, List Position )
    -> ( Model, List Position, Cmd Msg )
postScore ( model, past, blownUp ) =
    ( model
    , <span class="fragment __highlight-current">List.append past blownUp</span>
    , <span class="fragment __highlight-current">sendScoreUpdate (List.length blownUp) model.cable</span>
    )
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>sendScoreUpdate</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
sendScoreUpdate : Int -> ActionCable Msg -> Cmd Msg
sendScoreUpdate int cable =
    if int > 0 then
        Result.withDefault Cmd.none <|
            ActionCable.perform
                "scoreUpdate"
                [ ( "score", JE.int int ) ]
                identifier
                cable
    else
        Cmd.none
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>Tick</kbd>: <kbd>regenerateAsteroids</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
regenerateAsteroids : List Position -> Cmd Msg -> Cmd Msg
regenerateAsteroids asteroids existingCmd =
    let
        regen _ =
            Random.generate
                GenerateAsteroid
                <span class="fragment __highlight-current">asteroidPositionGenerator</span>
    in
        asteroids
            |> List.map regen
            |> (::) existingCmd
            |> Cmd.batch
  </code></pre>
</section>



<section>
  <h2>Updates</h2>
  <h3><kbd>asteroidPositionGenerator</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
asteroidPositionGenerator : Random.Generator Position
asteroidPositionGenerator =
    let
        xGenerator =
            Random.float 500.0 1500.0

        yGenerator =
            Random.float -220.0 220.0
    in
        Random.map2 Position xGenerator yGenerator
  </code></pre>
</section>


<section>
  <h2><kbd>Tick</kbd></h2>

  <h3>done!</h3>
  <p class="fragment">Are we there yet?</p>
</section>


<section>
  <h2>Update</h2>

  <p>Two left:</p>
  <ul>
    <li><kbd>GenerateAsteroid</kbd></li>
    <li><kbd>DataReceived</kbd></li>
  </ul>
</section>

<section>
  <h2>Updates</h2>
  <h3><kbd>GenerateAsteroid</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
        GenerateAsteroid position ->
            ( { model | asteroids = position :: model.asteroids }
            , Cmd.none
            )
  </code></pre>
</section>


<section>
  <h2>Updates</h2>
  <h3><kbd>DataReceived</kbd></h3>

  <pre><code data-trim data-noescape class="elm">
        DataReceived _ jsonValue ->
            ( dataReceived jsonValue model, Cmd.none )
  </code></pre>

  <pre><code data-trim data-noescape class="elm">
dataReceived : JD.Value -> Model -> Model
dataReceived json model =
    json
        |> JD.decodeValue (JD.field "score" JD.int)
        |> Result.toMaybe
        |> (\newScore -> { model | score = newScore })
  </code></pre>
</section>

<section>
  <h2>Updates</h2>
  <h3>Done!</h3>

  <p class="fragment">Now, on to the view!</p>
</section>

<section>
  <h2>View</h2>

  <pre><code data-trim data-noescape class="elm">
view : Model -> Html Msg
view model =
    Element.toHtml <|
        C.collage 1000 500 <|
            gameItems model
</code></pre>
</section>


<section>
  <h2>View Items</h2>

  <pre><code data-trim data-noescape class="elm">
gameItems : Model -> List C.Form
gameItems model =
    List.concat
        [ [ space
          , connectedSignal model
          , scoreView model.score
          , shipView model.myPosition
          ]
        , List.map asteroidView model.asteroids
        , Maybe.withDefault [] <|
            Maybe.map (laserView >> List.singleton) model.laser
        ]
  </code></pre>
</section>


<section>
    <h2>View Items</h2>
    <h3>space</h3>

    <pre><code data-trim data-noescape class="elm">
space : C.Form
space =
    C.rect 1000 500
        |> C.filled Color.black
  </code></pre>
</section>

<section>
  <h2>View Items</h2>
  <h3>Connected?</h3>

  <pre><code data-trim data-noescape class="elm">
connectedSignal : Model -> C.Form
connectedSignal { cable } =
    let
      colorFill =
        if Dict.isEmpty <| ActionCable.subscriptions cable then
            Color.gray
        else
            Color.green
    in
        C.circle 5.0
            |> C.filled colorFill
            |> C.move ( 490, -240 )
  </code></pre>
</section>


<section>
  <h2>View Items</h2>
  <h3>Score</h3>

  <pre><code data-trim data-noescape class="elm">
scoreView : Maybe Int -> C.Form
scoreView int =
    int
        |> Maybe.map toString
        |> Maybe.withDefault "?"
        |> (++) "Score: "
        |> Text.fromString
        |> Text.color Color.white
        |> C.text
        |> C.move (( 420.0, 240 ))
  </code></pre>
</section>

<section>
  <h2>View Items</h2>
  <h3>Ship and Laser</h3>

  <pre><code data-trim data-noescape class="elm">
shipView : Float -> C.Form
shipView myPosition =
    C.polygon [ ( -10, -5 ), ( -10, 5 ), ( 10, 0 ) ]
        |> C.filled Color.white
        |> C.move ( leftEdge, (250.0 / 100.0 * myPosition) )


laserView : Position -> C.Form
laserView laser =
    C.rect 20.0 2.0
        |> C.filled Color.white
        |> C.move ( laser.x, laser.y )
  </code></pre>
</section>


<section>
  <h2>View Items</h2>
  <h3>Asteroids</h3>

  <pre><code data-trim data-noescape class="elm">
asteroidView : Position -> C.Form
asteroidView position =
    C.ngon 7 asteroidRadius
        |> C.filled Color.gray
        |> C.move ( position.x, position.y )
  </code></pre>
</section>


<section>
  <h1>So close!</h1>
  <h2 class="fragment">Just gotta kick it off</h2>
</section>


<section>
  <h2>Init</h2>

  <pre><code data-trim data-noescape class="elm">
init : ( Model, Cmd Msg )
init =
    ( { myPosition = 0.0
      , laser = Nothing
      , asteroids = []
      , score = Nothing
      , error = Nothing
      , cable = <span class="fragment __highlight-current">initCable</span>
      , keysDown = Set.empty
      }
    , <span class="fragment __highlight-current">requestRandomAsteroidPositions</span>
    )
  </code></pre>
</section>

<section>
  <h2><kbd>initCable</kbd></h2>

  <pre><code data-trim data-noescape class="elm">
initCable : ActionCable Msg
initCable =
    ActionCable.initCable "ws://localhost:3000/cable"
        |> ActionCable.withDebug True
        |> ActionCable.onWelcome (Just CableConnected)
        |> ActionCable.onDidReceiveData (Just DataReceived)
  </code></pre>
</section>

<section>
  <h2>initialize asteroids</h2>

  <pre><code data-trim data-noescape class="elm">
requestRandomAsteroidPositions : Cmd Msg
requestRandomAsteroidPositions =
    Random.generate GenerateAsteroid asteroidPositionGenerator
        |> List.repeat 12
        |> Cmd.batch

  </code></pre>
</section>

<section>
  <h1>Done!</h1>
  <p><small>Ignoring those little helper functions</small></p>
</section>

<section>
  <h1>Thank you!</h1>
  <p>Brad Grzesiak: @listrophy</p>
  <p>Bendyworks: @bendyworks</p>
  <p>Elm: elm-lang.org</p>
</section>



			</div>
		</div>

		<script src="lib/js/head.min.js"></script>
		<script src="js/reveal.js"></script>

		<script>
			// More info https://github.com/hakimel/reveal.js#configuration
			Reveal.initialize({
				history: true,

				// More info https://github.com/hakimel/reveal.js#dependencies
				dependencies: [
					{ src: 'plugin/markdown/marked.js' },
					{ src: 'plugin/markdown/markdown.js' },
					{ src: 'plugin/notes/notes.js', async: true },
					{ src: 'plugin/highlight/highlight.js', async: true, callback: function() { hljs.initHighlightingOnLoad(); } }
				]
			});
		</script>
	</body>
</html>
